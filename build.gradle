plugins {
    id "org.jetbrains.kotlin.multiplatform" version "1.3.72"
}
repositories {
    mavenCentral()
}
group "com.example"
version "0.0.1"

apply plugin: "maven-publish"

kotlin {
    jvm()
//    linuxX64("linux")
    js {
        browser()
        nodejs()
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin("stdlib-common")
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.3.5"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin("test-common")
                implementation kotlin("test-annotations-common")
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin("stdlib-jdk8")
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin("test")
                implementation kotlin("test-junit")
            }
        }
        jsMain {
            dependencies {
                implementation kotlin("stdlib-js")
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:1.3.5"
            }
        }
        jsTest {
            dependencies {
                implementation kotlin("test-js")
            }
        }
    }
}

tasks.withType(AbstractTestTask) {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println()
            println "-" * 80
            println "Results: ${result.resultType} " +
                    "(" +
                    "${result.testCount} tests, " +
                    "${result.successfulTestCount} passed, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped" +
                    ")"
        }
    }
}
